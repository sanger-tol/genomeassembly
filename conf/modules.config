/*
re114
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Config file for defining DSL2 per module options and publishing paths
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Available keys to override module options:
        ext.args   = Additional arguments appended to command in module.
        ext.args2  = Second set of arguments appended to command in module (multi-tool modules).
        ext.args3  = Third set of arguments appended to command in module (multi-tool modules).
        ext.prefix = File name prefix for output files.
----------------------------------------------------------------------------------------
*/

/*
    Local functions for determining output directories and file names.
    Not a particularly great solution - not compliant with strict syntax.

    Replace ASAP with the new output publishing!
*/
mf {
    assemblyDir = { meta, starttime ->
        def asm_dir_name = "hifiasm"
        if (meta.assembly_type == "hic_phased") {
            asm_dir_name = "hifiasm-hic"
        } else if (meta.assembly_type == "trio_binned") {
            asm_dir_name = "hifiasm-trio"
        }

        def date = "${starttime.format('yyyyMMdd')}"
        return "${meta.id}.${asm_dir_name}.${date}"
    }

    subDir = { meta ->
        def subdir_name = ""
        if (meta.assembly_stage == "purged") {
            subdir_name = "purging"
        } else if (meta.assembly_stage == "polished") {
            subdir_name "polishing"
        } else if (meta.assembly_stage == "scaffolded") {
            subdir_name = "scaffolding_${meta._hap}/yahs/out.break.yahs"
        }

        return subdir_name
    }

}

process {

    withName: 'CONCATENATE_ASSEMBLIES' {
        tag        = { "${meta.id}_${meta.assembly_type}" }
        ext.prefix = { "${meta.id}_${meta.assembly_type}" }
    }

    withName: 'SEQKIT_GREP_SPLIT_HAPS' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        tag        = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        ext.args   = {
            meta._hap == "hap1" ? "-rp '^h1tg|^ptg'" : "-rp '^h2tg|^atg|^hap_'"
        }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/polishing" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:GENOME_STATISTICS:ASMSTATS' {
        ext.prefix = { "${assembly.getName()}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/" + "${mf.subDir(meta)}" + "/" },
            mode: params.publish_dir_mode,
            pattern: '*.stats'
        ]
    }

    withName: '.*:GENOME_STATISTICS:GFASTATS' {
        ext.prefix = { "${assembly.getName()}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/" + "${mf.subDir(meta)}" + "/" },
            mode: params.publish_dir_mode,
            pattern: '*.assembly_summary'
        ]
    }

    withName: '.*:GENOME_STATISTICS:BUSCO_BUSCO' {
        publishDir = [
            path: {
                [
                    "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/",
                    "${mf.subDir(meta)}",
                    "/${fasta.getBaseName()}.${lineage}.busco/"
                ].join("")
            },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.endsWith('busco.log') ? filename :
                    filename.endsWith('full_table.tsv') ? filename :
                    filename.endsWith('missing_busco_list.tsv') ? filename :
                    filename.startsWith('short_summary') ? filename :
                    filename.endsWith('busco.batch_summary.txt') ? filename :
                    null }
        ]
    }

    withName: '.*:GENOME_STATISTICS:MERQURYFK_MERQURYFK' {
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/" + "${mf.subDir(meta)}" + "/${assembly.getBaseName()}.ccs.merquryfk/" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:KMERS:FASTK_FASTK' {
        ext.args   = { "-k${params.kmer_size} -t -P." }
        ext.prefix = { "${meta.id}.${meta.read_type}.${params.kmer_size}" }
        tag        = { "${meta.id}.${meta.read_type}.${params.kmer_size}" }
        publishDir = [
            path: { "${params.outdir}/kmer" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:KMERS:FASTK_HISTEX' {
        ext.prefix = { "${meta.id}.${meta.read_type}.${meta.kmer_size}" }
        ext.args   = "-G"
        tag        = { "${meta.id}.${meta.read_type}.${meta.kmer_size}" }
        publishDir = [
            path: { "${params.outdir}/kmer" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:KMERS:GENOMESCOPE2' {
        ext.prefix = { "${meta.id}.${meta.read_type}.${meta.kmer_size}" }
        ext.args   = { "-k${meta.kmer_size}"                            }
        tag        = { "${meta.id}.${meta.read_type}.${meta.kmer_size}" }
        publishDir = [
            path: { "${params.outdir}/kmer" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:KMERS:MERQURYFK_HAPMAKER' {
        ext.prefix = { "${meta.id}.${meta.read_type}.${meta.kmer_size}" }
        tag        = { "${meta.id}.${meta.read_type}.${meta.kmer_size}" }
        publishDir = [
            path: { "${params.outdir}/kmer/trio" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:KMERS:YAK_COUNT' {
        ext.prefix = { "${meta.id}_${meta.read_type}"     }
        ext.args   = { "-k${params.kmer_size}"            }
        tag        = { "${meta.id}_yak_${meta.read_type}" }
        publishDir = [
            path: { "${params.outdir}/kmer" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:ORGANELLE_ASSEMBLY:MITOHIFI_FINDMITOREFERENCE' {
        errorStrategy = 'ignore'
        ext.args      = { "--min_length ${params.mitohifi_min_ref_len}" }
    }

    withName: '.*:ORGANELLE_ASSEMBLY:MITOHIFI_MITOHIFI_READS' {
        errorStrategy = 'ignore'
        ext.args = {
            [
                "-o ${meta.mitochondrial_code}",
                params.mitohifi_reads_args ?: ""
            ].join(' ').trim()
        }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/mito.reads" },
            mode: 'copyNoFollow',
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:ORGANELLE_ASSEMBLY:MITOHIFI_MITOHIFI_CONTIGS' {
        errorStrategy = 'ignore'
        ext.args = {
            [
                "-o ${meta.mitochondrial_code}",
                params.mitohifi_contigs_args ?: ""
            ].join(' ').trim()
        }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/mito" },
            mode: 'copyNoFollow',
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:ORGANELLE_ASSEMBLY:OATK' {
        ext.args = {
            [
                "-k ${params.oatk_kmer_size}",
                "-c " + Math.ceil(params.oatk_coverage ?: meta.coverage * 5),
                "-Ttmp"
            ].join(' ').trim()
        }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/mito.oatk" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:POLISHING_10X:BCFTOOLS_CONSENSUS' {
        // Filter by mapping quality, keep alt-alt het and alt-alt hom,
        // keep longer allele
        ext.args = '-i\'QUAL>1 && (GT="AA" || GT="Aa")\' -Hla'
        ext.prefix = { "${meta.id}_${meta.assembly_type}.consensus" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/polishing" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.contains('consensus.fa') ? "${meta.id}.consensus.fa" : null }
        ]
    }

    withName: '.*:POLISHING_10X:BCFTOOLS_INDEX_FB' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta.chunk_id}" }
        ext.args   = { "--tbi" }
    }

    withName: '.*:POLISHING_10X:BCFTOOLS_INDEX_NORM' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta.chunk_id}" }
        ext.args   = { "--tbi" }
    }

    withName: '.*:POLISHING:BCFTOOLS_NORM' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}_normalised" }
        ext.args = '--no-version'
    }

    withName: '.*:POLISHING_10X:BCFTOOLS_SORT' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta.chunk_id}_sorted" }
        scratch = true
    }

    withName: '.*:POLISHING_10X:BCFTOOLS_VIEW' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta.chunk_id}_view" }
        // Dont keep command line information, keep reference allele or
        // suggested alternative if reference is N
        ext.args = '--no-version -e\'type="ref"||REF~"N"\''
    }

    withName: '.*:POLISHING_10X:FREEBAYES' {
        ext.prefix = { "${meta.id}.${meta.assembly_type}"       }
        ext.args   = { "--skip-coverage ${meta.longranger_cov}" }
        tag        = { "${meta.id}.${meta.assembly_type}"       }
        publishDir = [
            enabled: false
        ]
    }

    withName: '.*:POLISHING_10X:GATK4_MERGE_FREEBAYES' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}_merged" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/polishing" },
            mode: params.publish_dir_mode,
            pattern: "*merged*",
            saveAs: { filename -> filename.contains("*_merged.vcf.gz*") ? 'merged.vcf' : null }
        ]
    }

    withName: '.*:POLISHING_10X:GAWK_BED_CHUNKS' {
        ext.suffix = { "bed"                                       }
        ext.args   = {
            [
                "-v prefix=${meta.id}_${meta.assembly_type}",
                "-v chunk_size=${params.polishing_n_chunks_bed}"
            ].join(' ').trim()
        }
        tag        = { "${meta.id}_${meta.assembly_type}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/polishing/chunks/" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:POLISHING_10X:LONGRANGER_MKREF' {
        tag        = { "${meta.id}_${meta.assembly_type}" }
        // ext.prefix = { "${meta.id}_${meta.assembly_type}" }
        container  = params.polishing_longranger_container_path
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/polishing" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:POLISHING_10X:LONGRANGER_ALIGN' {
        tag        = { "${meta.id}_${meta.assembly_type}" }
        // ext.prefix = { "${meta.id}_${meta.assembly_type}" }
        container  = params.polishing_longranger_container_path
        ext.args   = "--disable-ui --nopreflight"
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/polishing" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:POLISHING_10X:SAMTOOLS_FAIDX' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}" }
        tag        = { "${meta.id}_${meta.assembly_type}" }
    }

    withName: '.*:PURGING:CAT_PURGED_HAPS_TO_ALT' {
        ext.prefix = { "${meta.id}.purged.htigs.all.fa"   }
        tag        = { "${meta.id}_${meta.assembly_type}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/purging" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.contains('purged.htigs.all.fa') ? 'purged.htigs.all.fa' : null }
        ]
    }

    withName: '.*:PURGING:MINIMAP2_ALIGN_ASSEMBLY' {
        ext.args   = { "-xasm5 -DP -I" + Math.ceil(reads.size()/1e9) + "G" }
        ext.prefix = { "${meta.id}_${meta.assembly_type}.self_aln"         }
        tag        = { "${meta.id}_${meta.assembly_type}"                  }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/purging/coverage" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:PURGING:MINIMAP2_ALIGN_READS' {
        ext.args = {
            [
                //these options are from pbmm2 CSS preset
                "-k19 -w10 -O5,56 -E4,1 -A2 -B5 -z400,50 -r2000 --lj-min-ratio 0.5",
                "-I" + Math.ceil(reference.size()/1e9) + "G"
            ].join(' ').trim()
        }
        ext.prefix = { "${meta.id}_${meta2.assembly_type}.reads" }
        tag        = { "${meta.id}_${meta2.assembly_type}"       }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/purging/coverage" },
            mode: params.publish_dir_mode,
            pattern: ".*paf"
        ]
    }

    withName: '.*:PURGING:PURGEDUPS_PBCSTAT' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}" }
        tag        = { "${meta.id}_${meta.assembly_type}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/purging/coverage" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:PURGING:PURGEDUPS_CALCUTS' {
        ext.args = {
            def cov = meta.coverage + (meta.coverage / 2)
            def l = ""
            def m = "-m ${cov}"
            def u = "-u ${cov * 4}"
            if(params.purging_cutoffs) {
                def cut_list = params.purging_cutoffs.tokenize(",")
                if(cut_list.size() != 3) {
                    log.error("Error: only ${cut_list.size()} cutoffs provided instead of 3.")
                }
                l = "-l ${cut_list[0]}"
                m = "-m ${cut_list[1]}"
                u = "-u ${cut_list[2]}"
            }

            return "${l} ${m} ${u}".trim()
        }
        ext.prefix = { "${meta.id}_${meta.assembly_type}" }
        tag        = { "${meta.id}_${meta.assembly_type}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/purging/coverage" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:PURGING:PURGEDUPS_PURGEDUPS' {
        ext.args   = "-2"
        tag        = { "${meta.id}_${meta.assembly_type}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/purging/purge_dups" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:PURGING:PURGEDUPS_SPLITFA' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}.self_aln" }
        tag        = { "${meta.id}_${meta.assembly_type}"          }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/purging/split_aln" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:PURGING:PURGEDUPS_GETSEQS' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}" }
        tag        = { "${meta.id}_${meta.assembly_type}" }
        ext.args   = { params.purging_purge_middle ? "" : "-e" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/purging" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.contains('purged.fa') ? 'purged.fa' : null }
        ]
    }

    withName: '.*:RAW_ASSEMBLY:GAWK_GFA_TO_FASTA' {
        ext.prefix = { "${input.toString() - ~/\.gfa\.gz/}"        }
        ext.suffix = { "fa"                                        }
        tag        = { "${meta.id}_${meta.assembly_type}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:RAW_ASSEMBLY:HIFIASM' {
        ext.prefix = { "${meta.id}"                       }
        tag        = { "${meta.id}_${meta.assembly_type}" }
        ext.args   = {
            [
                params.hifiasm_error_correction_options ? "${params.hifiasm_error_correction_options}" : "",
                params.hifiasm_assembly_options ? "${params.hifiasm_assembly_options}" : ""
            ].join(" ").trim()
        }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:RAW_ASSEMBLY:HIFIASM_BIN' {
        ext.prefix = { "${meta.id}" }
        tag        = { "${meta.id}" }
        ext.args   = {
            [
                "--bin-only",
                params.hifiasm_error_correction_options ? "${params.hifiasm_error_correction_options}" : ""
            ].join(" ").trim()
        }
        publishDir = [
            path: { "${params.outdir}/${meta.id}.hifiasm.${workflow.start.format('yyyyMMdd')}/" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:SCAFFOLDING:HIC_MAPPING:BWAMEM2_INDEX' {
        tag = { "${meta.id}_${meta.assembly_type}" }
    }

    withName: '.*:SCAFFOLDING:HIC_MAPPING:HICCRAMALIGN_BWAMEM2ALIGN' {
        ext.args1  = ""
        ext.args2  = "-F0xB00 -nt"
        ext.args3  = "-5SPp"
        ext.args4  = "-mpu"
        ext.args5  = "-q ${params.hic_mapping_minq} -F 0x904"
        ext.args6  = "--write-index -l1"
        ext.prefix = { "${cram.getName()}.${meta.id}_${meta.assembly_type}_${meta._hap}.${chunkn}" }
        tag        = { "${cram.getName()}.${meta.id}_${meta.assembly_type}_${meta._hap}.${chunkn}" }
    }

    withName: '.*:SCAFFOLDING:HIC_MAPPING:HICCRAMALIGN_MINIMAP2ALIGN' {
        ext.args1  = ""
        ext.args2  = "-F0xB00 -nt"
        ext.args3  = "-ax sr --no-pairing"
        ext.args4  = "-mpu"
        ext.args5  = "-q ${params.hic_mapping_minq} -F 0x904"
        ext.args6  = "--write-index -l1"
        ext.prefix = { "${cram.getName()}.${meta.id}_${meta.assembly_type}_${meta._hap}.${chunkn}" }
        tag        = { "${cram.getName()}.${meta.id}_${meta.assembly_type}_${meta._hap}.${chunkn}" }
    }

    withName: '.*:SCAFFOLDING:HIC_MAPPING:MINIMAP2_INDEX' {
        tag      = { "${meta.id}_${meta.assembly_type}" }
        ext.args = {  "-I" + Math.ceil(fasta.size()/1e9)+"G" }
    }

    withName: '.*:SCAFFOLDING:HIC_MAPPING:SAMTOOLS_INDEX' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        tag        = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
    }

    withName: '.*:SCAFFOLDING:HIC_MAPPING:SAMTOOLS_FAIDX' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        tag        = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
    }

    withName: '.*:SCAFFOLDING:HIC_MAPPING:SAMTOOLS_MARKDUP' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        tag        = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/scaffolding_${meta._hap}" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.contains('*.bam') ? "merge.mkdup.bam" : null }
        ]
    }

    withName: '.*:SCAFFOLDING:HIC_MAPPING:SAMTOOLS_MERGE' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta._hap}_merged" }
        tag        = { "${meta.id}_${meta.assembly_type}_${meta._hap}"        }
    }

    withName: '.*:SCAFFOLDING:HIC_MAPPING_STATS:SAMTOOLS_FLAGSTAT' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/scaffolding_${meta._hap}" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:SCAFFOLDING:HIC_MAPPING_STATS:SAMTOOLS_INDEX' {
        ext.prefix = { "${input.getName()}" }
        tag        = { "${input.getName()}" }
    }

    withName: '.*:SCAFFOLDING:HIC_MAPPING_STATS:SAMTOOLS_IDXSTATS' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        tag        = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/scaffolding_${meta._hap}" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:SCAFFOLDING:HIC_MAPPING_STATS:SAMTOOLS_STATS' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        tag        = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/scaffolding_${meta._hap}" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*:SCAFFOLDING:SCAFFOLDING_YAHS:BAMTOBED_SORT' {
        ext.args1  = { '-u -F0x400' }
        ext.args2  = ""
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/scaffolding_${meta._hap}" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*SCAFFOLDING:SCAFFOLDING_YAHS:COOLER_CLOAD' {
        // Positions in the input file are zero-based;
        // chrom1 field number (one-based) is 2;
        // pos1 field number (one-based) is 3;
        // chrom2 field number (one-based) is 6;
        // pos2 field number (one-based) is 7
        ext.args = '-0 -c1 2 -p1 3 -c2 4 -p2 5'
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/scaffolding_${meta._hap}/yahs/out.break.yahs" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*SCAFFOLDING:SCAFFOLDING_YAHS:GAWK_PROCESS_PAIRS_FILE' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta._hap}.no_chr_sizes" }
        ext.suffix = "pairs.gz"
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/scaffolding_${meta._hap}/yahs/out.break.yahs" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*SCAFFOLDING:SCAFFOLDING_YAHS:JUICERTOOLS_PRE' {
        errorStrategy = "ignore"
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/scaffolding_${meta._hap}/yahs/out.break.yahs" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*SCAFFOLDING:SCAFFOLDING_YAHS:MAKE_PAIRS_FILE' {
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
    }


    withName: '.*SCAFFOLDING:SCAFFOLDING_YAHS:PRETEXTMAP' {
        ext.args = ''
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/scaffolding_${meta._hap}/yahs/out.break.yahs" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*SCAFFOLDING:SCAFFOLDING_YAHS:PRETEXTSNAPSHOT' {
        // Make one plot containing all sequences
        ext.args = '--sequences \"=full\"'
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/scaffolding_${meta._hap}/yahs/out.break.yahs" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }

    withName: '.*SCAFFOLDING:SCAFFOLDING_YAHS:YAHS' {
        ext.args = {
            [
                params.yahs_break_contigs ? "" : "--no-contig-ec",
                params.yahs_resolutions ? "-r ${params.yahs_resolutions}" : "",
                params.yahs_min_contig_length ? "-l ${params.yahs_min_contig_length}" : ""
            ].join(' ').trim()
        }
        ext.prefix = { "${meta.id}_${meta.assembly_type}_${meta._hap}" }
        publishDir = [
            path: { "${params.outdir}/${mf.assemblyDir(meta, workflow.start)}/scaffolding_${meta._hap}/yahs/out.break.yahs" },
            mode: params.publish_dir_mode,
            saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
        ]
    }
}
